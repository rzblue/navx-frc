plugins {
  id 'cpp'
  id 'java'
  id 'google-test'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
  id 'edu.wpi.first.NativeUtils' version '2023.6.1'
  id 'edu.wpi.first.GradleVsCode' version '1.3.0'
  id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2023.0.0'
}

repositories {
  mavenCentral()
}
if (project.hasProperty('releaseMode')) {
  wpilibRepositories.addAllReleaseRepositories(project)
} else {
  wpilibRepositories.addAllDevelopmentRepositories(project)
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// Apply C++ configuration
apply from: 'config.gradle'

// Apply Java configuration
dependencies {
    implementation 'edu.wpi.first.cscore:cscore-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.cameraserver:cameraserver-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.ntcore:ntcore-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.wpilibj:wpilibj-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.wpiutil:wpiutil-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.wpimath:wpimath-java:2023.1.1-beta-3'
    implementation 'edu.wpi.first.hal:hal-java:2023.1.1-beta-3'
    implementation "org.ejml:ejml-simple:0.41"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.12.4"
    implementation "com.fasterxml.jackson.core:jackson-core:2.12.4"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.4"
    implementation 'edu.wpi.first.thirdparty.frc2023.opencv:opencv-java:4.6.0-2'
}

// Set up exports properly
nativeUtils {
  exportsConfigs {
    // Main library is just default empty. This will export everything
    NavX {
    }
  }
}

model {
  components {
    NavX(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }
      nativeUtils.useRequiredLibrary(it, 'wpilib_shared')
    }
  }
  testSuites {
    NavXTest {
        sources.cpp {
            source {
                srcDir 'src/test/native/cpp'
                include '**/*.cpp'
            }
        }

        nativeUtils.useRequiredLibrary(it, "wpilib_executable_shared", "googletest_static")
    }
  }
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion '7.5.1'
}
